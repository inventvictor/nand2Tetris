// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    //BUILTIN Or16Way;

    PARTS:
   // Put you code here:
   Mux16(a[0..15]=x, b=false, sel=zx, out[0..15]=xzx);
   Mux16(a[0..15]=y, b=false, sel=zy, out[0..15]=yzy);
   Not16(in[0..15]=xzx, out[0..15]=notxzx);
   Not16(in[0..15]=yzy, out[0..15]=notyzy);
   Mux16(a[0..15]=xzx, b[0..15]=notxzx, sel=nx, out[0..15]=xnx);
   Mux16(a[0..15]=yzy, b[0..15]=notyzy, sel=ny, out[0..15]=yny);
   Add16(a[0..15]=xnx, b[0..15]=yny, out[0..15]=xnxplusyny);
   And16(a[0..15]=xnx, b[0..15]=yny, out[0..15]=xnxandyny);
   Mux16(a[0..15]=xnxandyny, b[0..15]=xnxplusyny, sel=f, out[0..15]=xyf);
   Not16(in[0..15]=xyf, out[0..15]=notxyf);
   Mux16(a[0..15]=xyf, b[0..15]=notxyf, sel=no, out[0..7]=fxyf, out[8..15]=sxyf, out[15]=ng, out[0..15]=out);
   Or8Way(in[0..7]=fxyf, out=fpart);
   Or8Way(in[0..7]=sxyf, out=spart);
   Or(a=fpart, b=spart, out=fors);
   Not(in=fors, out=zr);
   //////////   
   }